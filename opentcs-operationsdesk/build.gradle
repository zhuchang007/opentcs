// SPDX-FileCopyrightText: The openTCS Authors
// SPDX-License-Identifier: MIT

apply from: "${rootDir}/gradle/java-project.gradle"
apply from: "${rootDir}/gradle/java-codequality.gradle"
apply from: "${rootDir}/gradle/guice-application.gradle"
apply from: "${rootDir}/gradle/publishing-java.gradle"

if (!hasProperty('mainClass')) {
  ext.mainClass = 'org.opentcs.operationsdesk.RunOperationsDesk'
}
application.mainClass = ext.mainClass

ext.collectableDistDir = new File(buildDir, 'install')

dependencies {
  api project(':opentcs-commadapter-loopback')
  api project(':opentcs-common')
  api project(':opentcs-impl-configuration-gestalt')
  api project(':opentcs-plantoverview-common')
  api project(':opentcs-plantoverview-panel-loadgenerator')
  api project(':opentcs-plantoverview-panel-resourceallocation')
  api project(':opentcs-plantoverview-themes-default')
  api project(':uwant-common')

  runtimeOnly libs.slf4j.jdk14
}

compileJava {
  options.compilerArgs << "-Xlint:-rawtypes"
}

distributions {
  main {
    contents {
      from "${sourceSets.main.resources.srcDirs[0]}/org/opentcs/operationsdesk/distribution"
    }
  }
}

// For now, we're using hand-crafted start scripts, so disable the application
// plugin's start script generation.
startScripts.enabled = false

distTar.enabled = false

task release {
  dependsOn build
  dependsOn installDist
}

run {
  systemProperties(['java.util.logging.config.file':'./config/logging.config',\
                    'sun.java2d.d3d':'false',\
                    'opentcs.base':'.',\
                    'opentcs.home':'.',\
                    'opentcs.configuration.reload.interval':'10000',\
                    'opentcs.configuration.provider':'gestalt'])
  jvmArgs('-XX:-OmitStackTraceInFastThrow',\
          '-splash:bin/splash-image.gif')
}
